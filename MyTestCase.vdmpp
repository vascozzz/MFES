class MyTestCase
/*
  Superclass for test classes, simpler but more practical than VDMUnit`TestCase. 
  Provided by Professor João Carlos Pascoal Faria.
*/

operations

	-- Simulates assertion checking by reducing it to pre-condition checking.
	-- If 'arg' does not hold, a pre-condition violation will be signaled.
	protected assertTrue: bool ==> ()
	assertTrue(arg) == 
		return 
	pre arg;
  
	-- Simulates assertion checking by reducing it to post-condition checking.
	-- If values are not equal, prints a message in the console and generates 
	-- a post-conditions violation.
	protected assertEqual: ? * ? ==> ()
	assertEqual(expected, actual) == 
		if expected <> actual then (
  		IO`print("Actual  : (");
  		IO`print(actual); 
  		IO`println(")");
  		IO`print("Expected: (");
  		IO`print(expected);
  		IO`println(")\n")
		)
	post expected = actual
  
end MyTestCase
